group 'org.java.monocle.example'
version '1.0-SNAPSHOT'

apply plugin: 'java'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

/*
 * Allows you to run the UI tests in headless mode by calling gradle with the -Pheadless=true argument
 */
if (project.hasProperty('headless') ? project.headless : false) {
    println "Running UI Tests Headless"
    test {
        jvmArgs = ['-Djava.awt.headless=true', '-Dtestfx.robot=glass', '-Dtestfx.headless=true', '-Dprism.order=sw', '-Dprism.text=t2k']
    }
}

dependencies {
    // Junit dependency need to be sync with testfx library. Check https://github.com/TestFX/TestFX for more information.
    testCompile "junit:junit:4.10"

    // Library for integration testing for JavaFX.
    testCompile "org.testfx:testfx-core:4.0.+"
    testCompile "org.testfx:testfx-legacy:4.0.+", {
        exclude group: "junit", module: "junit"
    }

    // Using monocle just to avoid mouse freeze and other windows freeze during test
    testRuntime 'org.testfx:openjfx-monocle:1.8.0_20'
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }
        resources {
            srcDir 'src/main/resources'
            exclude '*.pyc'
        }
    }
    test {
        java {
            srcDirs = ['src/test/java', 'src/integrationTest/java']
        }
        resources {
            srcDirs = ['src/test/resources', 'src/integrationTest/resources']
            exclude 'src/test/resources/log4j.properties'
        }
    }
}

test {
    filter {
        includeTestsMatching "*Test*"
    }

    systemProperty "zw.username", System.getProperty("zw.username")
    systemProperty "zw.password", System.getProperty("zw.password")
}

